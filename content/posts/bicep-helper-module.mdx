---
title: Create Helper Modules with Azure Bicep
description: Use helper modules to assist with various repetitive values like IP address formats, dates, or even naming conventions.
date: '2023-11-20'
---

<Image src='/blog-post-4.jpg' width='718' height='404' alt='Image' />

### In this article

- [What is Azure Bicep](#what-are-bicep-modules)?
- What are Bicep Modules?
- Why should I use Modules?
- Summary

## What is Azure Bicep?

[Bicep](https://learn.microsoft.com/en-us/azure/azure-resource-manager/bicep/overview?tabs=bicep) is a domain-specific language (DSL) that uses a declarative syntax to deploy Azure resources. It provides concise syntax, reliable type safety, and support for code reuse.

```bicep
param location string = resourceGroup().location
param storageAccountName string = 'toylaunch${uniqueString(resourceGroup().id)}'

resource storageAccount 'Microsoft.Storage/storageAccounts@2021-06-01' = {
  name: storageAccountName
  location: location
  sku: {
    name: 'Standard_LRS'
  }
  kind: 'StorageV2'
  properties: {
    accessTier: 'Hot'
  }
}
```

A Bicep module used for deploying a Standard Azure Storage Account.

---

<h2 id='what-are-bicep-modules'>What are Bicep modules?</h2>

Modules are Bicep files that are deployed from another Bicep file which improve the readability of your code by encapsulating complex details of your deployment. Modules also unlock the ability to reuse code for different deployments. To learn more, you can [read the documentation](https://learn.microsoft.com/en-us/azure/azure-resource-manager/bicep/modules).

```bicep
module stgModule '../storageAccount.bicep' = {
  name: 'storageDeploy'
  params: {
    storagePrefix: 'examplestg1'
  }
}
```

Basic syntax for defining a Storage Account module.

---

## Why should I use modules?

Bicep modules will improve readability, reusability, and flatten your files. If this wasn't reason enough, you can create _helper modules_. A helper module is a Bicep module that does not deploy an Azure resource, but can provide you with reusable values for your resources.

This is helpful when you need certain values that are similar, but not the same, throughout your code base. Some examples are IP addresses, dates, and resource naming conventions.

### Problem

Let's take IP addresses as an example. IP addresses require a certain format which is not always consistent between resources, leading to a significant challenge in infrastructure management. An incorrect format can result in deployment errors or connectivity issues, causing potential downtime. For example:

```bicep
module stgModule '../storageAccount.bicep' = {
  name: 'storageDeploy'
  params: {
    storagePrefix: 'examplestg1'
    networkAcls: {
      defaultAction: 'deny'
      // ipRules: IPRule[]
      ipRules: [
        {
          action: 'Allow'
          value: '192.0.2.0/24'
        }
      ]
    }
  }
}
```

IPRule objects are made up of two values: action and value. Action is the action of the IP rule (`'Allow' | 'Deny'`), and value specifies the IP or IP range in [CIDR](https://en.wikipedia.org/wiki/Classless_Inter-Domain_Routing) format.

The previous IP address configuration is of course valid for storage accounts, but this wouldn't work for CosmosDB.

```bicep
module cosmos '../cosmos.bicep' = {
  name: cosmosAccountName
  params: {
    cosmosAccountName: cosmosAccountName
    virtualNetworkRules: []
    ipRules: [
      {
        ipAddressOrRange: '192.0.2.0/24'
      }
    ]
  }
}
```

### Solution

To avoid error prone IP address configurations or a long list of variables are the top of all your Bicep files, you can make use of helper modules.

```bicep
// ip-range-helper.bicep

var whiteListedIps = [
  {
    Name: 'Rule1'
    CIDR: '192.0.2.0/24'
    StartIP: '192.0.2.0'
    EndIP: '192.0.2.255'
  }
]

var cosmosFormat = [for address in whiteListedIps: {
  ipAddressOrRange: address.CIDR
}]

var storageFormat = [for address in whiteListedIps: {
  value: address.CIDR
  action: 'Allow'
}]

output cosmosFormat array = cosmosFormat
output storageFormat array = storageFormat
```

Now to consume this module:

```bicep
// main.bicep

module ipRangeHelper '../ip-range-helper.bicep' = {
  name: 'ip-ranges'
}

module stgModule '../storageAccount.bicep' = {
  name: 'storageDeploy'
  params: {
    storagePrefix: 'examplestg1'
    networkAcls: {
      defaultAction: 'deny'
      ipRules: ipRangeHelper.outputs.storageFormat
    }
  }
}

module cosmos '../cosmos.bicep' = {
  name: cosmosAccountName
  params: {
    cosmosAccountName: cosmosAccountName
    virtualNetworkRules: []
    ipRules: ipRangeHelper.outputs.cosmosFormat
  }
}
```

---

## Summary

Bicep modules are handy for improving readability and reusability in your code, but are also powerful tools for creating helpers. These helpers allow you to define a consistent formatting one time which can then be used everywhere.
